# cng: from userspace-rcu

# Project version information
m4_define([lmdb_version_major], [0])
m4_define([lmdb_version_minor], [9])
m4_define([lmdb_version_patch], [29])
m4_define([lmdb_version_dev_stage], [])
m4_define([lmdb_version], lmdb_version_major[.]lmdb_version_minor[.]lmdb_version_patch[]lmdb_version_dev_stage)

# Library version information of "liblmdb"
# Following the numbering scheme proposed by libtool for the library version
# http://www.gnu.org/software/libtool/manual/html_node/Updating-version-info.html
m4_define([lmdb_lib_version_current], [0])
m4_define([lmdb_lib_version_revision], [0])
m4_define([lmdb_lib_version_age], [0])
m4_define([lmdb_lib_version], lmdb_lib_version_current[:]lmdb_lib_version_revision[:]lmdb_lib_version_age)


##                     ##
## Autoconf base setup ##
##                     ##

AC_PREREQ([2.69])
AC_INIT([liblmdb], [lmdb_version])

AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([mdb.c])
AC_CONFIG_MACRO_DIR([m4])

AC_CONFIG_HEADERS([config.h])

AC_CANONICAL_TARGET
AC_CANONICAL_HOST


##                     ##
## Automake base setup ##
##                     ##

# no-extra-portability silences "archiver requires 'AM_PROG_AR' in 'configure.ac'" warnings
AM_INIT_AUTOMAKE([1.12 -Wall -Werror -Wno-extra-portability foreign dist-zstd no-dist-gzip])


##                               ##
## OS and Arch specific defaults ##
##                               ##

AS_CASE([$host],
  [*-cygwin*], [LT_NO_UNDEFINED="-no-undefined" CC_NO_ROBUST="-DMDB_USE_ROBUST=0"]
)


##                   ##
## C compiler checks ##
##                   ##

# Choose the C compiler
AC_PROG_CC
# AC_PROG_CC_STDC was merged in AC_PROG_CC in autoconf 2.70
m4_version_prereq([2.70], [], [AC_PROG_CC_STDC])

# Make sure we have pthread support
AX_PTHREAD([], [AC_MSG_ERROR([Could not configure pthread support])])

# Detect warning flags supported by the C compiler and append them to
# WARN_CFLAGS.
# these are from the original lmdb Makefile
m4_define([WARN_FLAGS_LIST], [ dnl
  -Wall dnl
  -Wno-unused-parameter dnl
  -Wbad-function-cast dnl
  -Wuninitialized dnl
])

# Pass -Werror as an extra flag during the test: this is needed to make the
# -Wunknown-warning-option diagnostic fatal with clang.
AC_LANG_PUSH([C])
AX_APPEND_COMPILE_FLAGS([WARN_FLAGS_LIST], [WARN_CFLAGS], [-Werror])
AC_LANG_POP([C])

# Initialize and configure libtool
LT_INIT


##                                             ##
## Substitute variables for use in Makefile.am ##
##                                             ##

# Library versions for libtool
AC_SUBST([LMDB_LIBRARY_VERSION], [lmdb_lib_version])

AC_SUBST(LT_NO_UNDEFINED)
AC_SUBST(CC_NO_ROBUST)

AM_CFLAGS="$WARN_CFLAGS $PTHREAD_CFLAGS"
AC_SUBST(AM_CFLAGS)


##                                     ##
## Output files generated by configure ##
##                                     ##

AC_CONFIG_FILES([
	Makefile
	liblmdb.pc
])
AC_OUTPUT
